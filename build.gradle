buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://artifactory.does.not.exist/artifactory/gradle-release'
        }
    }

    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

plugins {
    id 'com.github.sherter.google-java-format' version '0.6'
    id "org.sonarqube" version "2.6.2"
    id 'java'
}

def getCurrentGitBranch() {
    def gitBranch = "Unknown branch"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim().replaceAll('/','_')
            if (gitBranch == 'develop') {
                gitBranch = 'SNAPSHOT'
            }
        }
    } catch (e) {
    }
    return gitBranch
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'
    group = 'uk.gov.dft.bluebadge'
    version = rootProject.file('VERSION').text.trim() + '-' + getCurrentGitBranch()
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://artifactory.does.not.exist/artifactory/gradle-release-dev'
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

artifactory {
    contextUrl = "https://artifactory.does.not.exist/artifactory"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            maven = true
        }

        publishArtifacts=true
        publishPom=true
        defaults {
            publications('project_common')
            publishArtifacts=true
            publishPom=true
        }
    }

    resolve {
        repository {
            repoKey = 'gradle-release'
            maven = true

        }
    }

}

archivesBaseName = "bluebadge-common"
compileJava.dependsOn 'googleJavaFormat'

publishing {
    publications {
        project_common(MavenPublication) {
            from components.java
            artifactId "${archivesBaseName}"
        }
    }
}